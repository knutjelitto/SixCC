@namespace SixTools.Grammars
@accessibility internal
@classname SixgParser
@using Ast

grammar <Grammar>
    = _ "grammar" id:identifier _ ";" rules:ruleList EOF    { new Grammar(id, rules) }

ruleList <IEnumerable<Rule>>
    = rule*

rule <Rule>
    = id:rule_identifier _ ":" ex:expression _ ";"          { new Rule(id, ex) }
    / id:rule_identifier                                    #error{ "rule1" }
    / _ letter                                              #error{ "rule2" }

expression <Term>
    = a:alternatives                                        { a }

alternatives <Term>
    = first:sequence rest:(<Term> _ "|" r:sequence { r })*  { TermBuilder.Alternatives(first, rest) }

sequence <Term>
    = es:element*                                           { TermBuilder.Sequence(es) }

element <Term>
    = range
    / e:primary s:suffix                                    { TermBuilder.Group(e,s) }

suffix <RepetitionSuffix>
    = _ '?' (_ '?')?    { RepetitionSuffix.ZeroOrOne }
    / _ '*' (_ '?')?    { RepetitionSuffix.ZeroOrMore }
    / _ '+' (_ '?')?    { RepetitionSuffix.OneOrMore }
    /                   { RepetitionSuffix.None }

primary <Term>
    = identifier
    / string
    / character
    / _ '.'                                                 { new AnyTerm() }
    / _ "<" e:expression _ ">"                              { new TerminalTerm(e) }
    / _ "(" e:expression _ ")"                              { new ClampedTerm(e) }
    / _ "~" a:primary                                       { new NotTerm(a) }

range <Term>
    = a:string _ ".." o:string                              { new RangeTerm(a, o) }

rule_identifier <TokenTerm>
    = _ id:("%" identifier)                                 { new TokenTerm(id) }
    / _ id:identifier                                       { id }

identifier <TokenTerm>
    = _ id:(id_start id_continue*)                          { new TokenTerm(id) }

id_start
    = letter (letter / digit)*

id_continue
    = ("-"/"_") (letter / digit)+

string <LiteralTerm>
    = _ ("\'" s:single_char* "\'")                          { new LiteralTerm(s) }


character <LiteralTerm>
    = _ esc:(simple_escape / zero_escape / hex_escape / unicode_escape) { new LiteralTerm(esc) }

single_char
    = !("'" / "\\" / eol_char) .
    / simple_escape
    / zero_escape
    / hex_escape
    / unicode_escape
    / eol_escape

simple_escape
    =   "\\" char:[abefnrtv'"\\]
        {
            @char
            .Replace("a", "\u0007")
            .Replace("b", "\u0008")
            .Replace("e", "\u001B")
            .Replace("f", "\u000C")
            .Replace("n", "\u000A")
            .Replace("r", "\u000D")
            .Replace("t", "\u0009")
            .Replace("v", "\u000B")
        }

zero_escape
    = "\\0" { "\u0000" }

hex_escape
    =   "\\x" digits:(hex_digit hex_digit)
        {
            char.ConvertFromUtf32(Convert.ToInt32(digits, 16))
        }

unicode_escape
    =   "\\u{" digits:(hex_digit hex_digit? hex_digit? hex_digit? hex_digit? hex_digit?) "}"
        {
            char.ConvertFromUtf32(Convert.ToInt32(digits, 16))
        }

eol_escape
    =   "\\" eol:eol { eol }

eol
    =   "\n"
    /   "\r\n"
    /   "\r"

eol_char
    = [\n\r]

letter
    = [a-z] / [A-Z]

digit
    = [0-9]

hex_digit
    = [0-9a-fA-F]

block_comment
    = '/*' (!'*/' .)* '*/'

line_comment
    = '//' (!eol_char .)*

comment
    = block_comment
    / line_comment

white
    = [ \t\r\n]

_   = (white / comment)*

EOF
    = _ !.
