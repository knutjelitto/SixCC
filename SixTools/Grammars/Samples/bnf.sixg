grammar bnf;

rule-list
    : rule* EOF
    ;

rule
    : name '::=' expression
    ;

expression
    : alternatives
    ;

alternatives
    : alternative ('|' alternative)*
    ;

alternative
    : element*
    ;

element
    : optional
    | zero-or-more
    | grouping
    | text
    | name
    ;

optional
    : '[' expression ']'
    ;

zero-or-more
    : '{' expression '}'
    ;

grouping
    : '(' expression ')'
    ;

text
    : id
    ;

name
    : '<' id '>'
    ;

id
    : ('a' .. 'z' | 'A' .. 'Z') (' ' | 'a' .. 'z' | 'A' .. 'Z' | '0' .. '9')*
    ;

%whitespace
    : (' ' | '\r' | '\n' | '\t')*
    ;
