grammar Json;

%start      = json;
%whitespace = ws;

json
    | element
    ;

value
    | object
    | array
    | string
    | number
    | 'true'
    | 'false'
    | 'null'
    ;

object
    | '{' members? '}'
    ;

members
    | member
    | member ',' members
    ;

member
    | string ':' element
    ;

array
    |'[' elements? ']'
    ;

elements
    | element
    | element ',' elements
    ;

element
    | value
    ;

string
    | < '"' character* '"' >
    ;

character
    | (('\u{0020}' .. '\u{10FFFF}') - '"') - '\\'
    | '\\' escape
    ;

escape
    | '"'
    | '\\'
    | '/'
    | 'b'
    | 'f'
    | 'n'
    | 'r'
    | 't'
    | 'v'
    | 'u' hex hex hex hex
    ;

hex
    | digit
    | 'A' .. 'F'
    | 'a' .. 'f'
    ;

number
    | < integer fraction? exponent? >
    ;

integer
    | digit
    | onenine digits
    | '-' digit
    | '-' onenine digits
    ;

digits
    | digit+
    ;

digit
    | '0'
    | onenine
    ;

onenine
    | '1' .. '9'
    ;

fraction
    | '.' digits
    ;

exponent
    | ('E'|'e') sign? digits
    ;

sign
    | '+'
    | '-'
    ;

ws
    | ' '
    | '\n'
    | '\r'
    | '\t'
    ;
