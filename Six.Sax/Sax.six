grammar Sax;

%start      = compilation-unit;
%whitespace = skip;
%keywords   = identifier;

%namespace  = 'Six.Sax';

compilation-unit
    | module-descriptor
    | code-unit
    ;

code-unit
    : namespace
    ;

namespace
    : prelude 'namespace' names ';' declarations
    ;

//*****************************************************************************
//
// MODULE
//

module-descriptor
    : 'module' names module-body
    ;

module-body
    : '{' module-body-element* '}'
    ;

module-body-element
    : module-import
    ;

module-import
    : 'import' names ';'
    ;

//*****************************************************************************
//
// USING
//

using-declaration
    : 'using' names using-elements
    ;

using-elements
    : '{' using-element-list? '}'
    ;

using-element-list
    : using-element (',' using-element)*
    ;

using-element
    | using-named
    | using-wildcard
    ;

using-named
    : using-name using-name-specifier? using-elements?
    ;

using-name
    : identifier
    ;

using-name-specifier
    : '=' identifier
    ;

using-wildcard
    : '...'
    ;
    
//*****************************************************************************
//
// MISC
//

name
    | identifier
    ;

names
    : name ('.' name)*
    ;

path
    : reference ('.' reference)*
    ;

reference
    : name generic-arguments?
    ;

generic-arguments
    : '<' generic-argument-list? '>'
    ;

generic-argument-list
    : generic-argument (',' generic-argument)*
    ;

generic-argument
    : type
    ;

generic-parameters
    : '<' generic-parameter-list? '>'
    ;

generic-parameter-list
    : generic-parameter (',' generic-parameter)*
    ;

generic-parameter
    : name
    ;


//*****************************************************************************
//
// DECLARATION
//

declarations
    : declaration*
    ;

declaration
    | using-declaration
    | function-declaration
    | attribute-declaration
    | class-declaration
    | interface-declaration
    | object-declaration
    | alias-declaration
    ;

function-declaration
    : prelude 'fun' type name parameters function-body
    ;

attribute-declaration
    : prelude 'val' type? name attribute-body
    ;

function-body
    : expression-body
    | block-body
    ;

attribute-body
    : expression-body
    | block-body
    ;

expression-body
    : function-specifier? ';'
    ;

function-specifier
    : '=>' expression
    ;

block-body
    : '{' declarations statements '}'
    ;

class-declaration
    : prelude 'class' name parameters? extends? case-types? block-body
    ;

interface-declaration
    : prelude 'interface' name parameters? extends? case-types? block-body
    ;

extends
    : ':' type
    ;

case-types
    : 'of' case-type-list
    ;

case-type-list
    : type ('|' type)*
    ;

object-declaration
    : prelude 'object' name extends? block-body
    ;

alias-declaration
    : prelude 'alias' name '=>' type ';'
    ;

//*****************************************************************************
//
// PRELUDE
//

prelude
    : string-literal? attribute*
    ;

attribute
    : name attribute-arguments?
    ;

attribute-arguments
    : '(' attribute-argument-list? ')'
    ;

attribute-argument-list
    : attribute-argument (',' attribute-argument)*
    ;

attribute-argument
    : string-literal
    | meta-reference
    ;

meta-reference
    : class-reference
    | interface-reference
    | function-reference
    ;

class-reference
    : 'class' names
    ;

interface-reference
    : 'interface' names
    ;


function-reference
    : 'function' names
    ;

//*****************************************************************************
//
// TYPE
//
type
    : unionlevel-type
    ;

unionlevel-type
    : union-type
    | intersectionlevel-type
    ;

union-type
    : unionlevel-type '|' intersectionlevel-type
    ;

intersectionlevel-type
    : intersection-type
    | primary-type
    ;

intersection-type
    : intersectionlevel-type '&' primary-type
    ;

primary-type
    : path
    | constructor
    ;

constructor
    : path arguments
    ;

parameters
    : '(' parameter-list? ')'
    ;

parameter-list
    : parameter (',' parameter)*
    ;

parameter
    : prelude type name
    ;

arguments
    : '(' argument-list? ')'
    ;

argument-list
    : argument (',' argument)*
    ;

argument
    : expression
    ;

//*****************************************************************************
//
// STATEMENT
//

statements
    : statement*
    ;

statement
    | return-statement
    ;

return-statement
    : 'return' expression? ';'
    ;

//*****************************************************************************
//
// EXPRESSION
//

expression
    : addlevel-expression
    ;

addlevel-expression
    | add-expression
    | sub-expression
    | mullevel-expression
    ;

add-expression
    : addlevel-expression '+' mullevel-expression
    ;

sub-expression
    : addlevel-expression '-' mullevel-expression
    ;

mullevel-expression
    | mul-expression
    | div-expression
    | rem-expression
    | primary-expression
    ;

mul-expression
    : mullevel-expression '*' primary-expression
    ;

div-expression
    : mullevel-expression '/' primary-expression
    ;

rem-expression
    : mullevel-expression '%' primary-expression
    ;

primary-expression
    : name
    | literal
    ;

literal
    | string-literal
    ;

string-literal
    | plain-string-literal
    | verbatim-string-literal
    ;
    

//*****************************************************************************
//
// LEXER
//

decimal-digits
    | decimal-digit ('_' | decimal-digit)*
    ;

hex-digits
    | hex-digit ('_' | hex-digit)*
    ;

binary-digits
    | binary-digit ('_' | binary-digit)*
    ;

exponent
    | ( 'e' | 'E' ) ( '+' | '-' )? decimal-digit*
    ;

literal-float
    | < decimal-digits '.' decimal-digits exponent? >
    ;

literal-natural
    | < decimal-digits | '#' hex-digits | '$' binary-digits >
    ;

literal-char
    | < '\'' char-part '\'' >
    ;

string-start
    | < '"' string-part '``' >
    ;

string-mid
    | < '``' string-part '``' >
    ;

string-end
    | < '``' string-part '`'* '"' >
    ;

plain-string-literal
    | < '"' string-part '`'* '"' >
    ;

verbatim-string-literal
    | < '"""' (~'"' | '"' ~'"' | '""' ~'"')* '"""' >
    ;

char-part
    | ( ~('\\' | '\'') | escape-sequence )*
    ;

string-part
    | ( ~('\\'| '"'|'`') | ('`' ~('`'|'"'|'\\')) | '`\\' escape-core | escape-sequence )*
    ;

escape-sequence
    | '\\' (~'{' | '{' (~'}')* '}' )
    ;

escape-core
    | ~'{'
    | '{' (~'}')* '}'?
    ;

skip
    | white
    | comment
    ;

white
    | ( ' ' | '\r' | '\t' | '\f' | '\n' )+
    ;

line-end
    | '\r' '\n'
    | '\n'
    | '\r'
    ;

line-ender
    | '\r'
    | '\n'
    ;

comment
    | line-comment
    | block-comment
    ;

line-comment
    | '//' (~line-ender)* line-end?
    ;   

block-comment
    | '/*' ( ~'*' | '*'+ ~('*'|'/') )* '*'+ '/'
    ;

identifier
    | < identifier-start identifier-part* >
    ;

identifier-start
    | '_'
    | 'a' .. 'z'
    | 'A' .. 'Z'
    ;
    
identifier-part
    | '_'
    | decimal-digit
    | letter
    ;

letter
    | 'a' .. 'z' 
    | 'A' .. 'Z' 
    | '\u{0080}' .. '\u{ffff}'
    ;

decimal-digit
    | '0' .. '9'
    ;

hex-digit
    | '0' .. '9'
    | 'A' .. 'F'
    | 'a' .. 'f'
    ;

binary-digit
    | '0' | '1'
    ;
